plugins {
    id 'java'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.3.4'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'com.jfrog.bintray' version '1.6'
}

description = 'Dynamic Java Encoder (NextGen)'

// version is moved to gradle.properties where release plugin expects it
group 'org.chiknrice'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    mavenCentral()
}

// project dependencies
dependencies {
    compile 'org.slf4j:slf4j-api:1.7.10'
    testCompile 'junit:junit:4.11', 'org.concordion:concordion:1.5.1', 'org.chiknrice:concordion-setvar-extension:1.1.4'
    testRuntime 'org.slf4j:slf4j-simple:1.7.10'
}

// publish to bintray repo
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['Release']
    pkg {
        repo = 'maven'
        name = 'djeng'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/chiknrice/${project.name}.git'
    }
}

// resource filtering
processResources {
    filesMatching('**/*.xsd') {
        filter {
            it.replace('@version@', project.property('version'))
        }
    }
}

// to align with tag expected by travis (in .travis.yml)
release {
    tagTemplate = 'v${version}'
}

// to configure concordion to output to spec dir
test {
    systemProperties['concordion.output.dir'] = "$reporting.baseDir/spec"
}

// for publishing jacoco coverage report
jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// for publishing as maven artifact
publishing {
    publications {
        Release(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    description project.description
                    url "https://github.com/chiknrice/${project.name}"
                    scm {
                        url "https://github.com/chiknrice/${project.name}"
                        connection "scm:git:git@github.com:chiknrice/${project.name}.git"
                        developerConnection "scm:git:git@github.com:chiknrice/${project.name}.git"
                    }
                    licenses {
                        license {
                            name 'GPL v3.0'
                            url 'http://www.gnu.org/licenses/gpl.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'chiknrice'
                            name 'Ian Bondoc'
                            email 'chiknrice@gmail.com'
                            url 'https://github.com/chiknrice'
                        }
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

